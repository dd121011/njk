apply plugin: 'com.android.application'
def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}
android {
    signingConfigs {
        config {
            keyAlias 'laike'
            keyPassword 'laikelaike'
            storeFile file('../doc/laike.keystore')
            storePassword 'laikelaike'
        }
    }
    compileSdkVersion 21
    buildToolsVersion "22.0.1"
    defaultConfig {
        applicationId "com.njk"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"

        // dex突破65535的限制
        multiDexEnabled true
        // 默认是umeng的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "Channel ID"]
    }
    buildTypes {
        release {
            // 移除无用的resource文件
            shrinkResources true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为nongjiake_v1.0_2015-01-15_wandoujia.apk
                        def fileName = "nongjiake_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    lintOptions{
        checkReleaseBuilds false
        abortOnError false
    }

    productFlavors {
        "laike" {}
        "wandoujia" {}
        "yingyonghui" {}
        "yingyongbao" {}
        "nduo" {}
        "mumayi" {}
        "anzhi" {}
        "360zhushou" {}
        "360yystroe" {}
        "xiaomi" {}
        "ppzhushou" {}
        "sogouzhushou" {}
        "baidu" {}
        "anzhuostroe" {}
        "91zhushou" {}
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile project(':ptr-lib')
    compile project(':volley')
    compile 'in.srain.cube:clog:1.0.2'
    compile 'in.srain.cube:cube-sdk:1.0.44.39-SNAPSHOT@aar'
    compile files('libs/gson-2.2.2.jar')
    compile files('libs/ormlite-android-4.48.jar')
    compile files('libs/ormlite-core-4.48.jar')
    compile files('libs/universal-image-loader-1.9.4-SNAPSHOT.jar')
    compile files('libs/umeng-update-v2.6.0.1.jar')
    compile files('libs/com.umeng.message.lib_v2.5.0.jar')
    compile files('libs/umeng-analytics-v5.6.3.jar')
    compile files('libs/httpmime-4.1.2.jar')
}
